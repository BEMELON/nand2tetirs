// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   
    // X
    Not16(in = x, out=NotX);
    And16(a=x, b=false, out= ZX);
    Not16(in=ZX, out=ZXNX);

    // Y
    Not16(in = y, out=NotY);
    And16(a=y, b=false, out=ZY);
    Not16(in=ZY, out=ZYNY);

    // X Y Multiplex
    Mux4Way16(a=x,  b=NotX, c=ZX,  d=ZXNX, sel[1]=zx, sel[0]=nx, out=X0);
    Mux4Way16(a=y, b=NotY, c=ZY, d=ZYNY, sel[1]=zy, sel[0]=ny, out=Y0);

    // X + Y | X & Y
    Add16(a=X0, b=Y0, out=X0PlusY0);
    And16(a=X0, b=Y0, out=X0AndY0);

    // F Multiplex
    Mux16(a=X0AndY0, b=X0PlusY0, sel=f, out=F0);

    // Not F, F
    Not16(in=F0, out=NotF0);

    // No Multiplex
    Mux16(a=F0, b=NotF0, sel=no, out=No0);
    
    // out, ng
    And16(a=No0, b=true, out=out, out[15] = ng);

    // zr
    Or16Way(in=No0, out=ngSig);
    Mux(a=true, b=false, sel=ngSig, out=zr);

}